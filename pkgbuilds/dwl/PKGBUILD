pkgname=dwl
pkgver=0.4.r27.960fd6f
pkgrel=1
pkgdesc="dwm for Wayland"
arch=('x86_64')
url="https://github.com/tunalad/dwl"
license=('GPL')
depends=('wlroots0.16')
makedepends=('git' 'wayland-protocols')
optdepends=('xorg-xwayland: for XWayland support')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
# append #branch=wlroots-next to build against wlroots-git
source=('git+https://github.com/tunalad/dwl'
    config.h)
    sha256sums=('SKIP' 'SKIP')

    prepare() {
        cd "$srcdir/${pkgname%-git}"
        # Use a custom config.h if the file is not empty
        if [ -s "$srcdir/config.h" ]; then
            cp -f "$srcdir/config.h" config.h
        fi
        # Uncomment to compile with XWayland support
        sed -i -e '/-DXWAYLAND/s/^#//' config.mk
    }

    build() {
        export CFLAGS="$CFLAGS -I/usr/include/wlroots0.16"
        export LDFLAGS="$LDFLAGS -L/usr/lib/wlroots0.16"
        cd "$srcdir/${pkgname%-git}"
        make
    }

    package() {
        cd "$srcdir/${pkgname%-git}"
        make PREFIX="$pkgdir/usr/" install
    }
